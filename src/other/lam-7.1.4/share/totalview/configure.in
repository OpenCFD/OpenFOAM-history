# -*- shell-script -*-
#
# Copyright (c) 2001-2003 The Trustees of Indiana University.  
#                         All rights reserved.
# Copyright (c) 1998-2001 University of Notre Dame. 
#                         All rights reserved.
# Copyright (c) 1994-1998 The Ohio State University.  
#                         All rights reserved.
# 
# This file is part of the LAM/MPI software package.  For license
# information, see the LICENSE file in the top level directory of the
# LAM/MPI source distribution.
#
# $Id: configure.in,v 1.5 2003/04/30 03:01:47 jsquyres Exp $
#

############################################################################
# Initialization, version number, and other random setup/init stuff
############################################################################

# Init autoconf

AC_INIT(./src/dll_lam.c)
AC_PREREQ(2.52)
AC_CONFIG_AUX_DIR(./config)

# Establish the top-level LAM directory

top_lam_srcdir='$(top_srcdir)/../..'
top_lam_builddir='$(top_builddir)/../..'

AC_SUBST(top_lam_srcdir)
AC_SUBST(top_lam_builddir)

# Get the version of LAM that we are installing

LAM_GET_VERSION($srcdir/../../config, $srcdir/../../VERSION, LAM)

# Use the package name "lam" so that $pkglibdir is the same as LAM's
# main pkglibdir

AM_INIT_AUTOMAKE(lam, $LAM_VERSION, 'no')

# $lam_top_srcdir/config/lam_function.m4
LAM_CONFIGURE_SETUP
LAM_BASIC_SETUP

# Figure out the host type and C compiler

# $lam_top_srcdir/config/lam_setup_cc.m4
# $lam_top_srcdir/config/lam_check_optflags.m4
LAM_SETUP_CC

# Ensure that we are building in 32 bit mode.  Really, we just can't
# build in 64 bit mode.  But defensive programming says to only build
# where we know that we can build, so instead of testing for !=64,
# test for !=32.  This may actually be more restrictive than
# necessary, but it's safer that way.

AC_ARG_ENABLE(tv-dll-force,
    AC_HELP_STRING([--enable-tv-dll-force],
		   [force the Totalview DLL to be built even in non-32 bit built environments]))
LAM_GET_SIZEOF([void *], lam_sizeof_void_p)
if test "$lam_sizeof_void_p" != 4; then
    AC_MSG_WARN([Detected a non-32 bit build platform.])
    AC_MSG_WARN([The Totalview DLL needs to be built in the same mode])
    AC_MSG_WARN([as the Totalview debugger.])
    if test "$enable_tv_dll_force" != "yes"; then
	AC_MSG_WARN([Since you did not specify --enable-tv-dll-force, I am])
	AC_MSG_WARN([making the safest assumption and will refuse to build])
	AC_MSG_WARN([the Totalview DLL.])
	AC_MSG_ERROR([Cannot continue])
    fi
    AC_MSG_WARN([--enable-tv-dll-force specified; building anyway])
fi

# Setup libtool to build the shared library (not the static library)

AM_ENABLE_SHARED
AM_DISABLE_STATIC
AM_PROG_LIBTOOL

# Party on

AC_CONFIG_FILES([
    Makefile
    config/Makefile
    src/Makefile
])
AC_OUTPUT
