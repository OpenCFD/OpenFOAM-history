# -*- shell-script -*-
#
# Copyright (c) 2001-2004 The Trustees of Indiana University.  
#                         All rights reserved.
# Copyright (c) 1998-2001 University of Notre Dame. 
#                         All rights reserved.
# Copyright (c) 1994-1998 The Ohio State University.  
#                         All rights reserved.
# 
# This file is part of the LAM/MPI software package.  For license
# information, see the LICENSE file in the top level directory of the
# LAM/MPI source distribution.
#
# $Id: configure.stub,v 1.2 2004/02/15 14:15:01 jsquyres Exp $
#


AC_DEFUN([SSI_CONFIGURE_STUB],[

#
# boot tm configure.stub
#

TMDIR=
AC_ARG_WITH(tm, 
	    AC_HELP_STRING([--with-tm=DIR],
			   [obsolete synonym for --with-boot-tm]),
            [TMDIR=$withval])
AC_ARG_WITH(boot-tm, 
	    AC_HELP_STRING([--with-boot-tm=DIR],
			   [directory where the tm software was installed]),
            [TMDIR=$withval])

if test "$with_tm" = "no" -o "$with_boot_tm" = "no" ; then
    AC_MSG_ERROR([*** Disabled by user])
fi

#
# Need to find tm.h - note that we don't care about CPPFLAGS being reset
# if this doesn't work, that's all she wrote :)
#
if test ! -z "$TMDIR"; then
    CPPFLAGS="$CPPFLAGS -I$TMDIR/include"
fi

AC_CHECK_HEADERS(tm.h,,
    AC_MSG_ERROR([*** Cannot find working tm.h.]))


#
# Some OS's need -lsocket -lnsl
#

AC_CHECK_LIB(socket, main, ,)
AC_CHECK_LIB(nsl, main, ,)

#
# Sometimes the TM library will be in $prefix/lib, sometimes it will
# be in $prefix/lib64.  Try them both.  Addtionally, Torque 2.1.0p0
# renamed their library from libpbs to libtorque.  So look there, too.
#
if test -n "$TMDIR" -a -d "$TMDIR/lib"; then
    LDFLAGS_orig="$LDFLAGS"
    LDFLAGS="$LDFLAGS -L$TMDIR/lib"
fi
AC_CHECK_LIB(pbs, tm_init, HAPPY=1 PBS_LIB="pbs", HAPPY=0)
if test "$HAPPY" = "0"; then
    AC_CHECK_LIB(torque, tm_init, HAPPY=1 PBS_LIB="torque", HAPPY=0)
fi

if test "$HAPPY" = "0" -a -n "$TMDIR" -a -d "$TMDIR/lib64"; then
    LDFLAGS="$LDFLAGS_orig -L$TMDIR/lib64"
    AC_CHECK_LIB(pbs, tm_finalize, HAPPY=1 PBS_LIB="pbs", HAPPY=0)
    if test "$HAPPY" = "0"; then
        AC_CHECK_LIB(torque, tm_finalize, HAPPY=1 PBS_LIB="torque", HAPPY=0)
    fi
fi

if test "$HAPPY" = "0"; then
    AC_MSG_ERROR([*** Cannot find working TM library.])
else
    LIBS="-l$PBS_LIB $LIBS"
fi

#
# done with boot tm configure.stub
#
])dnl
