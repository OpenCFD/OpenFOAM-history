#----------------------------------*-sh-*--------------------------------------
# =========                 |
# \\      /  F ield         | OpenFOAM: The Open Source CFD Toolbox
#  \\    /   O peration     |
#   \\  /    A nd           | Copyright (C) 1991-2009 OpenCFD Ltd.
#    \\/     M anipulation  |
#------------------------------------------------------------------------------
# License
#     This file is part of OpenFOAM.
#
#     OpenFOAM is free software; you can redistribute it and/or modify it
#     under the terms of the GNU General Public License as published by the
#     Free Software Foundation; either version 2 of the License, or (at your
#     option) any later version.
#
#     OpenFOAM is distributed in the hope that it will be useful, but WITHOUT
#     ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
#     FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License
#     for more details.
#
#     You should have received a copy of the GNU General Public License
#     along with OpenFOAM; if not, write to the Free Software Foundation,
#     Inc., 51 Franklin St, Fifth Floor, Boston, MA 02110-1301 USA
#
# Script
#     paraview3/cshrc
#
# Description
#     Setup file for paraview-3.x
#     Sourced from OpenFOAM-*/etc/cshrc
#
# Note
#     The env. variable 'ParaView_DIR' is required for building plugins
#------------------------------------------------------------------------------

# determine the cmake to be used
unsetenv CMAKE_HOME
foreach cmake ( cmake-2.6.4 cmake-2.6.2 cmake-2.4.6 )
    set cmake=$WM_THIRD_PARTY_DIR/$cmake/platforms/$WM_ARCH
    if ( -r $cmake ) then
        setenv CMAKE_HOME $cmake
        set path=($CMAKE_HOME/bin $path)
        break
    endif
end

set paraviewMajor=paraview-3.6
setenv ParaView_VERSION 3.6.1

setenv ParaView_INST_DIR $WM_THIRD_PARTY_DIR/paraview-$ParaView_VERSION
setenv ParaView_DIR $ParaView_INST_DIR/platforms/$WM_ARCH$WM_COMPILER

# add in python libraries if required
set paraviewPython=$ParaView_DIR/Utilities/VTKPythonWrapping
if ( -r $paraviewPython ) then
    if ($?PYTHONPATH) then
        setenv PYTHONPATH ${PYTHONPATH}:${paraviewPython}:$ParaView_DIR/lib/${paraviewMajor}
    else
        setenv PYTHONPATH ${paraviewPython}:$ParaView_DIR/lib/${paraviewMajor}
    endif
endif

if ( -r $ParaView_INST_DIR ) then
    set path=($ParaView_DIR/bin $path)
    setenv PV_PLUGIN_PATH $FOAM_LIBBIN
endif

unset cmake paraviewMajor paraviewPython
# -----------------------------------------------------------------------------
